{ lib
, pkgs
, config
, hardware
, ...
}:

let
  # A small script to print an emoji based on the time of day
  timeEmoji = pkgs.writeShellScript "time-emoji" ''
    HOUR=$(date +'%H')

    if (( $HOUR > 5 && $HOUR < 12 )); then
      echo "Óçå "
    elif (( $HOUR >= 12 && $HOUR < 21 )); then
      echo "Óåç "
    elif (( $HOUR >= 21 && $HOUR <= 23 )); then
      echo "Óçç "
    elif (( $HOUR > 23 || $HOUR <= 5 )); then
      echo "üåô"
    fi
  '';
in
{
  programs.i3status-rust = {
    enable = true;

    bars.top = {
      blocks = [
        {
          # Display the currently playing song from an MPRIS instance
          block = "custom";
          command = "${pkgs.mpris-statusbar}/bin/mpris-statusbar";
          hide_when_empty = true;
          interval = 1;
        }
        {
          # Temperature
          block = "custom";
          command = pkgs.writeShellScript "temperature" ''
            weather="$(${pkgs.curl}/bin/curl -S "wttr.in/?format=1" 2>/dev/null | tr -d "+" | xargs)"
            # This might contain long error codes if the service is down
            (( "''${#weather}" < 10 )) && echo "$weather"
          '';
          on_click = "$TERMINAL --hold -e curl 'https://wttr.in/?F'";
          hide_when_empty = true;
          interval = 200;
        }
        {
          # Ping time
          block = "custom";
          command = pkgs.writeShellScript "ping-time" ''
            time="$(${pkgs.unixtools.ping}/bin/ping store.steampowered.com -c 1 -w 1 2>/dev/null | ${pkgs.gnugrep}/bin/grep time= | ${pkgs.coreutils}/bin/cut -d'=' -f4)"
            # This might contain long error codes if the service is down
            (( "''${#time}" < 15 )) && (( "''${#time}" > 1 )) && echo "Ôá´  $time"
          '';
          hide_when_empty = true;
          interval = 1;
        }
        {
          block = "sound";
          format = "{volume}";
          on_click = "${pkgs.pavucontrol}/bin/pavucontrol";
        }
        {
          block = "cpu";
          format = "{utilization}";
          on_click = "$TERMINAL -e htop";
          interval = 1;
        }
        {
          block = "memory";
          format_mem = "{mem_used}";
          interval = 1;
        }
        {
          # Time block, but with an emoji refering to the time of day
          block = "custom";
          command = "echo \"$(${timeEmoji}) $(date +'%H:%M:%S')\"";
          interval = 1;
        }
      ] ++ lib.optionals (hardware.battery or false) [rec {
        block = "battery";
        interval = 10;
        format = " {percentage}";
        full_format = format;
      }];

      settings = {
        icons.overrides = {
          volume_full = "ÔÄ®";
          volume_half = "Ô©Ω";
          volume_empty = "ÔÄß";
          volume_muted = "ÔÄ¶";
          bat_charging = "Ôíí";
          bat_quarter = "ÔâÉ";
          bat_half = "ÔâÇ";
          bat_three_quarters = "ÔâÅ";
          bat_full = "ÔâÄ";
          bat_empty = "ÔâÑ";
          cpu = "ÔëØ";
          memory_mem = "Ôãõ";
          bat = "Battery:";
        };
      };
    };
  };
}
